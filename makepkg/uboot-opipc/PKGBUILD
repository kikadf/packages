# U-Boot: Orange Pi PC
# Maintainer: kikadf <kikadf.01@gmail.com> 

buildarch=4

pkgname=uboot-opipc
pkgver=2022.10
pkgrel=1
pkgdesc="U-Boot for Orange Pi PC"
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr' 'boot/config.txt')
makedepends=('bc' 'dtc' 'swig' 'python' 'python-setuptools')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'boot.txt'
        'mkscr')
md5sums=('cb1f3774f5e423b91cff379101c764a3'
         '95f60c0ae1315e986d8a2aee15d5f854'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver}
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> configs/orangepi_pc_defconfig
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  make distclean
  make orangepi_pc_defconfig
  make EXTRAVERSION=-${pkgrel}

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt ../boot.scr
}

package() {
  cd u-boot-${pkgver}

  install -d "${pkgdir}"/boot
  install -Dm644 u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  install -Dm644 ../boot.txt "${pkgdir}"/boot/boot.txt
  install -Dm644 ../boot.scr "${pkgdir}"/boot/boot.scr
  install -Dm755 ../mkscr "${pkgdir}"/boot/mkscr 
}
