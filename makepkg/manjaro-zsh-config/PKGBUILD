# Maintainer: Gregor Boehl <admin@gregorboehl.com>

pkgname=manjaro-zsh-config
pkgver=r77.1f9d0da
pkgrel=1
pkgdesc="Zsh configuration for manjaro"
arch=('any')
url="https://github.com/Chrysostomus/manjaro-zsh-config"
license=('MIT')
depends=('zsh-autosuggestions'
	'zsh-syntax-highlighting'
	'zsh-completions'
	'zsh-history-substring-search'
	'zsh'
	'pkgfile'
	'ttf-noto-nerd'
	'zsh-theme-powerlevel10k-git')
makedepends=('git')
backup=('root/.zshrc')
install="$pkgname.install"
source=("${pkgname}::git+https://github.com/Chrysostomus/manjaro-zsh-config.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$srcdir/$pkgname"
	install -D -m644 "$pkgname" -t "${pkgdir}/usr/share/zsh/"
	install -D -m644 manjaro-zsh-prompt -t "${pkgdir}/usr/share/zsh"
	install -D -m644 zsh-maia-prompt -t "${pkgdir}/usr/share/zsh/"
	install -D -m644 p10k.zsh -t "${pkgdir}/usr/share/zsh/"
	install -D -m644 p10k-portable.zsh -t "${pkgdir}/usr/share/zsh/"
	install -D -m640 .zshrc -t "${pkgdir}/etc/zsh/zshrc-manjaro"
	install -d "${pkgdir}/usr/share/zsh/scripts"
	cp -r base16-shell "${pkgdir}/usr/share/zsh/scripts/"
	chmod a+x "${pkgdir}/usr/share/zsh/scripts/base16-shell/"*
	install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/$pkgname/"

}
